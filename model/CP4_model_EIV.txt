model
{


####CP Regression model
  for(t in 1:n_years)
  {

  s_star[t]<- alpha[Kstar[t]+Lstar[t]+Mstar[t]]+beta[Jstar[t]+Kstar[t]+Lstar[t]+Mstar[t]]*(year.t[t]-cp[Kstar[t]+Lstar[t]+Mstar[t]])

  Jstar[t] <- step(year.t[t]-cp[1])
  Kstar[t]<- step(year.t[t]-cp[2])
  Lstar[t]<-step(year.t[t]-cp[3])
  Mstar[t]<- 1 + step(year.t[t]-cp[4])

   }

  ###Data Loop
  for(j in 1:n_obs)
  {
  y.j[j] ~ dnorm(alpha[K[j]+L[j]+M[j]]+beta[J[j]+K[j]+L[j]+M[j]]*(mu_year.j[j]-cp[K[j]+L[j]+M[j]]),sigma_err^-2)

  J[j] <- step(mu_year.j[j]-cp[1])
  K[j]<- step(mu_year.j[j]-cp[2])
  L[j]<- step(mu_year.j[j]-cp[3])
  M[j]<- 1 + step(mu_year.j[j]-cp[4])

  year.j[j]~dnorm(mu_year.j[j],year_sd[j]^-2)
  mu_year.j[j] ~ dnorm(0,0.0001)

  tau_y[j] <- (y_sd[j]^2 + sigma_err^2)^-1

}


  ##Priors
 alpha[1] ~ dnorm(0.0,0.01)
 alpha[2] ~ dnorm(0.0,0.01)
 alpha[3] ~ dnorm(0.0,0.01)
 alpha[4] ~ dnorm(0.0,0.01)



 beta[1]~dnorm(0.0,0.01)
 beta[2] <- (alpha[2] - alpha[1])/(cp[2]-cp[1])
 beta[3] <- (alpha[3] - alpha[2])/(cp[3]-cp[2])
 beta[4] <- (alpha[4] - alpha[3])/(cp[4]-cp[3])
 beta[5]~dnorm(0.0,0.01)


 sigma_err ~ dt(0,4^-2,1)T(0,)

 cp.temp[1] ~ dunif(year_min,year_max)
 cp.temp[2] ~ dunif(year_min,year_max)
 cp.temp[3] ~ dunif(year_min,year_max)
 cp.temp[4] ~ dunif(year_min,year_max)
 cp[1:4]<-sort(cp.temp)

}##End model
